#File Generated by Hans J. Johnson
#Please contact hans-johnson@uiowa.edu for making enhancments/corrections
cmake_minimum_required(VERSION 3.11 FATAL_ERROR)
include(FindZLIB)

set(NIFTI_LIB_NAME ${PACKAGE_PREFIX}znz)
add_library(${NIFTI_LIB_NAME} znzlib.c)
target_link_libraries( ${NIFTI_LIB_NAME} ZLIB::ZLIB )
target_include_directories(${NIFTI_LIB_NAME} PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
        )

# Set library version if building shared libs.
if(BUILD_SHARED_LIBS)
  set_target_properties(${NIFTI_LIB_NAME} PROPERTIES ${NIFTI_LIBRARY_PROPERTIES})
endif()


if(NOT NIFTI_INSTALL_NO_LIBRARIES)
  install(TARGETS ${NIFTI_LIB_NAME}
          EXPORT NIFTITargets
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT Development)
endif()

if(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
  file(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  install(FILES ${__files}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT Development)
endif()

