#File Generated by Hans J. Johnson
#Please contact hans-johnson@uiowa.edu for making enhancments/corrections
cmake_minimum_required(VERSION 3.11 FATAL_ERROR)
  
include(GNUInstallDirs)
find_library(MATH_LIBRARY m)
include(FetchContent)
FetchContent_Declare(
  znz
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../znzlib
)

FetchContent_GetProperties(znz)
if(NOT znz_POPULATED)
  FetchContent_Populate(znz)
  add_subdirectory(${znz_SOURCE_DIR} ${znz_BINARY_DIR})
endif()



set(NIFTILIB_SRC nifti1_io.c)

set(NIFTI_LIB_NAME ${PACKAGE_PREFIX}niftiio)
add_library(${NIFTI_LIB_NAME} ${NIFTILIB_SRC} )
# Set library version when building shared libs.
if(BUILD_SHARED_LIBS)
  set_target_properties(${NIFTI_LIB_NAME} PROPERTIES ${NIFTI_LIBRARY_PROPERTIES})
endif()

target_include_directories(${NIFTI_LIB_NAME} PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
        )
set_target_properties(${NIFTI_LIB_NAME} PROPERTIES PUBLIC_HEADER "nifti1_io.h;nifti1.h")
if(MATH_LIBRARY)
    target_link_libraries(${NIFTI_LIB_NAME} PUBLIC ${MATH_LIBRARY} znz)
else()
  target_link_libraries(${NIFTI_LIB_NAME} PUBLIC znz)
endif()


if(NOT NIFTI_INSTALL_NO_LIBRARIES)
  install(TARGETS ${NIFTI_LIB_NAME}
          EXPORT NIFTITargets
    PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT Development)
endif()

if(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
  file(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  install(FILES ${__files}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT Development)
endif()
